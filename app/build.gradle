apply plugin: 'com.android.application'

apply plugin: 'com.hiya.jacoco-android'
apply plugin: 'org.sonarqube'

android {
  compileSdkVersion 28
  buildToolsVersion "28.0.3"

  defaultConfig {
    // TODO change to:
    // applicationId "tech.gruppone.stalker.mobile-app"
    applicationId "com.gruppone.stalker"
    minSdkVersion 21
    //noinspection OldTargetApi
    targetSdkVersion 28
    versionCode 8
    versionName "0.5.2"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    buildConfigField("String", "ADMIN_API_KEY", "\"${ADMIN_API_KEY}\"")
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

      testCoverageEnabled true
    }

    debug {
      testCoverageEnabled true
    }
  }

  lintOptions {
    abortOnError true
    warningsAsErrors true

    checkAllWarnings true
    // use disable to individually remove lint checks that are redundant
    // disable "id1", "id2"
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation 'androidx.appcompat:appcompat:1.1.0'
  implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
  implementation 'androidx.lifecycle:lifecycle-viewmodel:2.2.0'
  implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

  testImplementation 'junit:junit:4.13'
  testImplementation 'org.mockito:mockito-core:3.3.3'
  testImplementation 'org.powermock:powermock-module-junit4:2.0.2'
  testImplementation 'org.powermock:powermock-api-mockito2:2.0.2'

  androidTestImplementation 'androidx.test.ext:junit:1.1.1'
  androidTestImplementation 'androidx.test:runner:1.2.0'
  androidTestImplementation 'androidx.test:rules:1.2.0'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
  androidTestImplementation 'org.mockito:mockito-android:2.7.22'

  implementation 'androidx.lifecycle:lifecycle-viewmodel:2.2.0'

  // TODO remove bootstrap (we decided to use material)
  implementation 'com.beardedhen:androidbootstrap:2.3.2'
  implementation 'com.android.volley:volley:1.1.1'

  implementation 'com.android.support:recyclerview-v7:28.0.0'

  implementation 'com.google.android.gms:play-services-location:17.0.0'

  implementation 'javax.vecmath:vecmath:1.5.2'

  compileOnly 'org.projectlombok:lombok:1.18.12'
  annotationProcessor 'org.projectlombok:lombok:1.18.12'
}

jacoco {
  toolVersion = "0.8.4"
}

// TODO how to enforce minimum coverage?

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

tasks.findByName('jacocoTestReport').mustRunAfter('test')

jacocoAndroidUnitTestReport {
  csv.enabled false
  html.enabled false
  xml.enabled true
}

project.tasks["check"].dependsOn "jacocoTestReport"
// project.tasks["sonarqube"].dependsOn "check"

sonarqube {
  properties {
    property "sonar.sourceEncoding", "UTF-8"

    property "sonar.projectName", "stalker-mobile-app"
    property "sonar.projectKey", "GruppOne_stalker-mobile-app"
    property "sonar.organization", "grupp-one"

    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.login", "${env.SONARCLOUD_LOGIN.orNull()}"

    property 'sonar.androidLint.reportPaths', "${buildDir}/reports/lint-results.xml"
    property 'sonar.coverage.jacoco.xmlReportPaths', "${buildDir}/jacoco/jacoco.xml"
  }
}
